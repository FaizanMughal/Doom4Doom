enum SpawnChances
{
	NoSpawnChanceProgressive =	150,
	NoSpawnChanceRandom = 		192,
};

//==============================================================================
//		Chainsaw
//==============================================================================
Class ChainsawSpawner : CustomInventory replaces Chainsaw	//Do not use randomspawner.
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			A_SpawnItemEx("D4Chainsaw",0,0,0,0,0,0,random(0,359),PFlags,0,tid); 
			
			if (GetCvar("MPWeapons") && !GetCvar("MPSpawnProgressive"))
			{
				A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,64);
			}
		}
		Stop;
	}
}

//===================================================================================================
//		Shotgun
//===================================================================================================
Class ShotgunSpawner : CustomInventory replaces Shotgun
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.Amount 0;
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			if (GetCvar("MPWeapons"))
			{
				if (GetCvar("MPSpawnProgressive"))	
				{
					if (CountInv("D4Shotgun",Player1) && random(1,2)==2 && CountInv("D4StaticRifle",Player1)==0)
					{	A_SpawnItemEx("D4StaticRifle",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION);	}
					else
					{	A_SpawnItemEx("D4Shotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
				else
				{	
					A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);	
					A_SpawnItemEx("D4Shotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);
				}
			}
			else
			{	A_SpawnItemEx("D4Shotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
		}
		Stop;
	}
}

//===================================================================================================
//		Super Shotgun
//===================================================================================================
Class SuperShotgunSpawner : CustomInventory replaces SuperShotgun	//Do not use randomspawner.
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	//int user_rng;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.Amount 0;
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			if (GetCvar("MPWeapons"))
			{
				if (GetCvar("MPSpawnProgressive"))	
				{
					if (CountInv("D4SuperShotgun",Player1) && random(1,2)==1)
					{	A_SpawnItemEx("D4VortexRifle",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION);	}
					else
					{	A_SpawnItemEx("D4SuperShotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
				else	//if MP weps == "random"
				{	
					A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);	
					A_SpawnItemEx("D4SuperShotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);
				}
			}
			else
			{	A_SpawnItemEx("D4SuperShotgun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
		}
		Stop;
	}
}

//===================================================================================================
//		Chaingun
//===================================================================================================
Class ChaingunSpawner : CustomInventory replaces Chaingun	//Do not use randomspawner.
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	int user_rng;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			if (GetCvar("MPWeapons"))
			{
				//if MP weps == "Progressive"
				if (GetCvar("MPSpawnProgressive"))	
				{
					if (CountInv("D4AssaultRifle",Player1))
					{	
						if(countinv("D4Repeater",Player1))	//if player has both HAR and repeater, chaingun will appear rarely
						{
							//random from 1 to 7
							// 1,2 & 3 = HAR
							// 4 & 5 = repeater
							// 6 = chaingun
						
							invoker.user_rng = random(1,6);
							if 		(invoker.user_rng == 6) { A_SpawnItemEx("D4Chaingun",0,0,0,0,0,0,random(0,359),PFlags,0,tid); }
							
							else if (invoker.user_rng >= 1 && invoker.user_rng <= 3) 
									{ A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid); }
									
							else	{ A_SpawnItemEx("D4Repeater",0,0,0,0,0,0,random(0,359),PFlags,0,tid); }
							
						}
						else
						{
							if(random(1,3)==3)
							{	A_SpawnItemEx("D4Repeater",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
							else
							{	A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
						}
					}
					else
					{	
						A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	
					}
				}
				//if MP weps == "random"
				else	
				{	
					A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);	
					if (random(1,8) == 1)	{	A_SpawnItemEx("D4Chaingun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
					else					{	A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
			}
			//if MP weps OFF
			else	
			{	
				if(countinv("D4AssaultRifle",Player1))
				{
					if (random(1,4) == 1)	{	A_SpawnItemEx("D4Chaingun",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
					else					{	A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
				else
				{
					A_SpawnItemEx("D4AssaultRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	
				}
			}
		}
		Stop;
	}
}

//===================================================================================================
//		Rocketlauncher
//===================================================================================================
Class RocketLauncherSpawner : CustomInventory replaces RocketLauncher
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			if (GetCvar("MPWeapons"))
			{
				//if MP weps == "progressive"
				if (GetCvar("MPSpawnProgressive"))
				{
					if(countinv("D4RocketLauncher",Player1) && countinv("D4GaussCannon",Player1))	//if player has both, then it's random
					{
						if(random(1,2)==1)
						{	A_SpawnItemEx("D4GaussCannon",0,0,0,0,0,0,random(0,359),PFlags,0,tid); }
						else
						{	A_SpawnItemEx("D4RocketLauncher",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
					}
					else //if player didnt have both RL and GC, then decides which one to spawn
					{
						if(countinv("D4RocketLauncher",Player1) && countinv("D4PlasmaRifle",Player1) || countinv("D4RocketLauncher",Player1) && countinv("D4LightningGun",Player1))
						{	
							A_SpawnItemEx("D4GaussCannon",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	
						}
						else
						{	A_SpawnItemEx("D4RocketLauncher",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
					}
				}
				
				//if MP weps == "random"
				else
				{	
					A_SpawnItemEx("D4RocketLauncher",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	//always
					A_SpawnItemEx("D4GaussCannon",0,0,0,0,0,0,random(0,359),PFlags,192,tid);	//sometimes
					A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);	//sometimes
				}
			}
			//if MP weps OFF
			else
			{
				if(countinv("D4RocketLauncher",Player1) && countinv("D4GaussCannon",Player1))	//if player has both, then it's random
				{
					if(random(1,2)==1)
					{	A_SpawnItemEx("D4GaussCannon",0,0,0,0,0,0,random(0,359),PFlags,0,tid); }
					else
					{	A_SpawnItemEx("D4RocketLauncher",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
				else
				{
					if(countinv("D4RocketLauncher",Player1) && countinv("D4PlasmaRifle",Player1))
					{	A_SpawnItemEx("D4GaussCannon",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
					else
					{	A_SpawnItemEx("D4RocketLauncher",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
			}
		}
		Stop;
	}
}

//===================================================================================================
//		Plasma rifle
//===================================================================================================
Class PlasmaRifleSpawner : CustomInventory replaces PlasmaRifle
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			if (GetCvar("MPWeapons"))
			{
				//if MP weps == "Progressive"
				if (GetCvar("MPSpawnProgressive"))	
				{
					if (CountInv("D4PlasmaRifle",Player1) && random(1,3)==1)
					{	A_SpawnItemEx("D4LightningGun",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION);	}
					else
					{	A_SpawnItemEx("D4PlasmaRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
				}
				//if MP weps == "random"
				else	
				{	
					A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,NoSpawnChanceRandom);	
					A_SpawnItemEx("D4PlasmaRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);
				}
			}
			//if MP weps OFF
			else
			{	A_SpawnItemEx("D4PlasmaRifle",0,0,0,0,0,0,random(0,359),PFlags,0,tid);	}
		}
		Stop;
	}
}

//==============================================================================
//		BFG 9000
//==============================================================================
Class BFGSpawner : CustomInventory replaces BFG9000
{
	const PFlags = SXF_NOCHECKPOSITION|SXF_CLEARCALLERTID|SXF_TRANSFERSPECIAL|SXF_CLEARCALLERSPECIAL|SXF_TRANSFERPOINTERS;
	Default 
	{
		+INVENTORY.ALWAYSPICKUP
		+INVENTORY.QUIET
		-COUNTITEM
		+NOTELESTOMP
		+NOTONAUTOMAP
		RenderStyle "None";
		Inventory.RespawnTics 1032;
	}
	States
	{
	Spawn:
		TNT1 A -1 NoDelay
		{
			A_SpawnItemEx("D4BFG",0,0,0,0,0,0,random(0,359),PFlags,0,tid); 
			
			if (GetCvar("MPWeapons"))
			{
				if (GetCvar("MPSpawnProgressive"))
				{
					//nothing
				}
				else
				{	A_SpawnItemEx("D4MPWeapon",0,0,0,0,0,0,random(0,359),SXF_NOCHECKPOSITION,64);	}
			}
		}
		Stop;
	}
}

//==============================================================================
//		Multiplayer Weapon Spawner
//==============================================================================
Class D4MPWeapon : Actor 
{
	int user_rng;
	Default 
	{
		+NOINTERACTION
		+NOTELESTOMP
		+NOBLOCKMAP
		RenderStyle "None";
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			user_rng = (CountInv("D4StaticRifle",Player1) ? random(1,3) : random(1,4));
				 if (user_rng == 1)	{	A_SpawnItemEx("D4LightningGun",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);	}
			else if (user_rng == 2)	{	A_SpawnItemEx("D4VortexRifle",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);	}
			else if (user_rng == 3)	{	A_SpawnItemEx("D4Repeater",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);		}
			else 					{	A_SpawnItemEx("D4StaticRifle",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);	}
		}
		Stop;
	}
}
