// For an example on replacing spawners, see the Fatso one.

Class D4DMonsterSpawner : Actor
{
	bool isStealth;
	bool goDormant;
	bool isBoss;
	Array<String> OldMonList;		// Classic
	Array<String> NewMonList;		// D4D
	Class<Actor> OldMonster;		// You get the drift...
	Class<Actor> NewMonster;
	Actor mon;
	property isBoss: isBoss;		// For monsters that trigger E*M8/MAP07 specials.
	property OldMonster: OldMonster;
	property NewMonster: NewMonster;
	property isStealth: isStealth;
	Default 
	{
		D4DMonsterSpawner.isBoss false;
		D4DMonsterSpawner.isStealth false;
		Monster;
		Health 1000;
		+DONTCORPSE
		+NORADIUSDMG
		+NONSHOOTABLE
		-SHOOTABLE
		-COUNTKILL
		-VULNERABLE
		+NOTIMEFREEZE
		+CANTSEEK
		+BOSSDEATH
		+NEVERTARGET
		+DONTTHRUST
		+DONTSPLASH
		+NOGRAVITY
		+NOPAIN
		+NOTRIGGER
		+NOTONAUTOMAP
		+NOTELEPORT
		+ALWAYSTELEFRAG
		+LAXTELEFRAGDMG
		+THRUACTORS
		+NOINTERACTION
		Species "D4DSpawner";
		DamageFactor "Normal", 0.0;
		DamageFactor "Massacre", 666;
	}
	
	override void Deactivate(Actor activator)
	{
		goDormant = true;
	}
	
	override void Activate(Actor activator)
	{
		goDormant = false;
	}
	
	override void PostBeginPlay()
	{
		// Don't ever do anything with its base self for safety reasons.
		
		if (GetClass() == "D4DMonsterSpawner")
			return Destroy();
		
		Super.PostBeginPlay();
		//======================================================================
		// Check to see if modders have injected monster classes to spawn.
		// Currently does not have an offset randomizer yet.
		//======================================================================
		int montype = GetCvar("D4D_MonsterType");
		// D4D Themed Section
		
		if (montype != 0)
		{
			if (NewMonList.Size() > 0)
			{
				for (int i = NewMonList.Size() - 1; i >= 0; i--)
				{
					Class<Actor> tester = NewMonList[i];
					if (!tester)
					{
						A_Log(NewMonList[i].." doesn't exist! Check your spelling/definitions!" );
						NewMonList.Pop();
					}
				}
				
				if (NewMonList.Size() > 0)
				{
					uint mMax = NewMonList.Size() - 1;
					NewMonster = NewMonList[random(0,mMax)];
					NewMonList.Clear();
				}
			}
		}
		// Classic Section
		else
		{
			if (OldMonList.Size() > 0)
			{
				for (int i = OldMonList.Size() - 1; i >= 0; i--)
				{
					Class<Actor> tester = OldMonList[i];
					if (!tester)
					{
						A_Log(OldMonList[i].." doesn't exist! Check your spelling/definitions!" );
						OldMonList.Pop();
					}
				}
				
				if (OldMonList.Size() > 0)
				{
					uint mMax = OldMonList.Size() - 1;
					OldMonster = OldMonList[random(0,mMax)];
					OldMonList.Clear();
				}
			}
		}
		
		// And get back to our daily spawning.
		Class<Actor> toSpawn = null;
		if (montype == 0 && OldMonster)
			toSpawn = OldMonster;
		else if (montype != 0 && NewMonster)
			toSpawn = NewMonster;
		else return Destroy();
		
		bool test;
		[test, mon] = A_SpawnItemEx(toSpawn,0,0,0,0,0,0,0,MonsterSpawnFlags,0,tid);
		
		if (mon)
		{
			if (isStealth)	mon.bSTEALTH = true;
			int rng = random(0, 1999);
			int amt = 0;
			if (monType > 1)
			{	
				if (rng > 800)
				{
						 if (rng >= 1999)	{	amt = 7;	mon.A_SetTranslation("D4D_White");	}
					else if (rng >= 1997)	{	amt = 6;	mon.A_SetTranslation("D4D_Black");	}
					else if (rng >= 1981)	{	amt = 5;	mon.A_SetTranslation("D4D_Red");	}
					else if (rng >= 1961)	{	amt = 4;	mon.A_SetTranslation("D4D_Orange");	}
					else if (rng >= 1931)	{	amt = 3;	mon.A_SetTranslation("D4D_Purple");	}
					else if (rng >= 1901)	{	amt = 2;	mon.A_SetTranslation("D4D_Blue");	}
					else if (rng >= 1701)	{	amt = 1;	mon.A_SetTranslation("D4D_Green");	}
				
					if (amt > 0)
					{
						int hp = mon.GetSpawnHealth();
						int amp = amt + 1;
						
						if (hp < 1000)	amp += 1;
						if (hp <= 500) 	amp += 1;
						if (hp <= 400) 	amp += 1;
						if (hp <= 300) 	amp += 1;
						if (hp <= 200) 	amp += 1;
						if (hp <= 100) 	amp += 1;
						
						mon.A_GiveInventory("D4MonsterUpgrader",amt);
						mon.A_SetHealth(hp * amp);
						mon.Mass *= (amt + 1);
						//mon.WoundHealth *= (amp + 1);
					}
				}
			}
			if (bBossSpawned)
			{
				mon.TeleportMove (mon.pos, true);
				mon.bBossSpawned = true;
			}
			//if (isBoss)
			//{
				let d4mon = D4AbstractMonster(mon);
				if (d4mon)
				{
					d4mon.Spawner = self;
						 if (amt < 0 || amt > 7) { }
					else if (amt == 7)	d4mon.BC = "White";
					else if (amt == 6)	d4mon.BC = "Black";
					else if (amt == 5)	d4mon.BC = "Red";
					else if (amt == 4)	d4mon.BC = "Orange";
					else if (amt == 3)	d4mon.BC = "Purple";
					else if (amt == 2)	d4mon.BC = "Blue";
					else if (amt == 1)	d4mon.BC = "Green";
					
				}
			//}
			
			if (master)	
				mon.master = master;
			
			if (goDormant) 
			{	
				mon.Deactivate(self);	
				//mon.A_ChangeCountFlags(false,-1,bCOUNTSECRET);
			}
			mon.A_ChangeCountFlags(true,-1,bCOUNTSECRET);
			
			/*
			if (health != GetSpawnHealth())	
				mon.A_SetHealth(health);
			*/
				
			A_ChangeLinkFlags(true, true);
			A_ChangeCountFlags(0);
		}
	}
	
	override void OnDestroy()
	{
		A_BossDeath();
		Super.OnDestroy();
	}
	
	States
	{
	Spawn:
		TNT1 A 70 NODELAY
		{
			if (!mon || mon.health < 1 || !isBoss)
			{
				bVULNERABLE = true;
				A_Die();
			}
		}
		Loop;
	Death:
		TNT1 AAAAA 35;
		Stop;
	}
}




//===============================================================================
//===============================================================================
//	Zombieman
//===============================================================================
//===============================================================================

Class ZombiemanReplacer : D4DMonsterSpawner replaces Zombieman
{
	Default 
	{
		// Use these for just single spawning types. Keeps code compact.
		// See fatso spawner below for example on how to spawn a random type.
		D4DMonsterSpawner.OldMonster "D4OldZombieman";
		D4DMonsterSpawner.NewMonster "D4Zombieman";
		Radius 20;
		Height 56;
	}
}

Class StealthZombiemanReplacer : ZombiemanReplacer  replaces StealthZombieman
{	Default	{	D4DMonsterSpawner.isStealth true;	}	}

//===============================================================================
//===============================================================================
//	ShotgunGuy
//===============================================================================
//===============================================================================

Class ShotgunGuyReplacer : D4DMonsterSpawner replaces ShotgunGuy
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldShotgunGuy";
		D4DMonsterSpawner.NewMonster "D4ShotgunGuy";
		Radius 20;
		Height 56;
	}
}

Class StealthShotgunGuyReplacer : ShotgunGuyReplacer  replaces StealthShotgunGuy
{ Default	{ D4DMonsterSpawner.isStealth true;	} }
//===============================================================================
//===============================================================================
//	ChaingunGuy
//===============================================================================
//===============================================================================

Class ChaingunGuyReplacer : D4DMonsterSpawner replaces ChaingunGuy
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldChaingunGuy";
		D4DMonsterSpawner.NewMonster "D4ChaingunGuy";
		Radius 20;
		Height 56;
	}
}

Class StealthChaingunGuyReplacer : ChaingunGuyReplacer  replaces StealthChaingunGuy
{ Default	{ D4DMonsterSpawner.isStealth true;	} }

//===============================================================================
//===============================================================================
//	Demon
//===============================================================================
//===============================================================================
Class DemonReplacer : D4DMonsterSpawner replaces Demon
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldDemon";
		D4DMonsterSpawner.NewMonster "D4Demon";
		Radius 30;
		Height 56;
	}
}

Class DemonStealthReplacer : DemonReplacer  replaces StealthDemon
{ Default	{ D4DMonsterSpawner.isStealth true;	} }

//===============================================================================
//===============================================================================
//	Spectre
//===============================================================================
//===============================================================================
Class SpectreReplacer : D4DMonsterSpawner replaces Spectre
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldSpectre";
		D4DMonsterSpawner.NewMonster "D4Spectre";
		Radius 30;
		Height 56;
	}
}
//===============================================================================
//===============================================================================
//	DoomImp
//===============================================================================
//===============================================================================
Class DoomImpReplacer : D4DMonsterSpawner replaces DoomImp
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldDoomImp";
		D4DMonsterSpawner.NewMonster "D4DoomImp";
		Radius 20;
		Height 56;
	}
}

Class StealthDoomImpReplacer : DoomImpReplacer  replaces StealthDoomImp
{ Default	{ D4DMonsterSpawner.isStealth true;	} }

//===============================================================================
//===============================================================================
//	LostSoul
//===============================================================================
//===============================================================================
Class LostSoulReplacer : D4DMonsterSpawner replaces LostSoul
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldLostSoul";
		D4DMonsterSpawner.NewMonster "D4LostSoul";
		Radius 16;
		Height 56;
	}
}
//===============================================================================
//===============================================================================
//	Cacodemon
//===============================================================================
//===============================================================================
Class CacodemonReplacer : D4DMonsterSpawner replaces Cacodemon
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldCacodemon";
		D4DMonsterSpawner.NewMonster "D4Cacodemon";
		Radius 31;
		Height 56;
	}
}

Class StealthCacodemonReplacer : CacodemonReplacer  replaces StealthCacodemon
{ Default	{ D4DMonsterSpawner.isStealth true;	} }
//===============================================================================
//===============================================================================
//	PainElemental
//===============================================================================
//===============================================================================
Class PainElementalReplacer : D4DMonsterSpawner replaces PainElemental
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldPainElemental";
		D4DMonsterSpawner.NewMonster "D4PainElemental";
		Radius 31;
		Height 56;
	}
}

//===============================================================================
//===============================================================================
//	Revenant
//===============================================================================
//===============================================================================
Class RevenantReplacer : D4DMonsterSpawner replaces Revenant
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldRevenant";
		D4DMonsterSpawner.NewMonster "D4Revenant";
		Radius 20;
		Height 56;
	}
}

Class StealthRevenantReplacer : RevenantReplacer  replaces StealthRevenant
{ Default	{ D4DMonsterSpawner.isStealth true;	} }


//===============================================================================
//===============================================================================
//	HellKnight
//===============================================================================
//===============================================================================
Class HellKnightReplacer : D4DMonsterSpawner replaces HellKnight
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldHellKnight";
		D4DMonsterSpawner.NewMonster "D4HellKnight";
		Radius 24;
		Height 64;
	}
}

Class StealthHellKnightReplacer : HellKnightReplacer  replaces StealthHellKnight
{ Default	{ D4DMonsterSpawner.isStealth true;	} }

//===============================================================================
//===============================================================================
//	BaronOfHell
//===============================================================================
//===============================================================================
Class BaronOfHellReplacer : D4DMonsterSpawner replaces BaronOfHell
{
	Default 
	{
		// Boss monster. Needs the isBoss boolean so it knows to stick around
		// until the monster's death.
		D4DMonsterSpawner.isBoss true;
		D4DMonsterSpawner.OldMonster "D4OldBaronOfHell";
		D4DMonsterSpawner.NewMonster "D4BaronOfHell";
		Radius 24;
		Height 64;
	}
}

Class StealthBaronReplacer : BaronOfHellReplacer  replaces StealthBaron
{ Default	{ D4DMonsterSpawner.isStealth true;	} }


//===============================================================================
//===============================================================================
//	Fatso
//===============================================================================
//===============================================================================
Class FatsoReplacer : D4DMonsterSpawner replaces Fatso
{
	Default 
	{
		D4DMonsterSpawner.isBoss true;
		D4DMonsterSpawner.OldMonster "D4OldFatso";
		// Intentionally didn't have a NewMonster here.
		Radius 24;
		Height 64;
	}
	
	// All modders need to do is override the PostBeginPlay. Depending on the setting,
	// simply add to NewMonList if the mode is D4D style or not (can also throw 
	// in your own cvar checks, that's totally fine).
	
	// Use the NewMonList.Push for D4D regular, and OldMonList.Push for classic.
	// Make sure to call the Super.PostBeginPlay() at the end of it.
	// However, if you wish to eliminate previous listings, then call
	// D4DMonsterSpawner.PostBeginPlay(); instead.
	override void PostBeginPlay()
	{
		NewMonList.Push("D4FatsoHell");
		NewMonList.Push("D4FatsoTech");
		Super.PostBeginPlay();
	}
}

Class StealthFatsoReplacer : FatsoReplacer  replaces StealthFatso
{ Default	{ D4DMonsterSpawner.isStealth true;	} }

//===============================================================================
//===============================================================================
//	Arachnotron
//===============================================================================
//===============================================================================
Class ArachnotronReplacer : D4DMonsterSpawner replaces Arachnotron
{
	Default 
	{
		D4DMonsterSpawner.isBoss true;
		D4DMonsterSpawner.OldMonster "D4OldArachnotron";
		D4DMonsterSpawner.NewMonster "D4Arachnotron";
		Radius 64;
		Height 64;
	}
}

Class StealthArachnotronReplacer : ArachnotronReplacer  replaces StealthArachnotron
{ Default	{ D4DMonsterSpawner.isStealth true;	} }


//===============================================================================
//===============================================================================
//	Archvile
//===============================================================================
//===============================================================================
Class ArchvileReplacer : D4DMonsterSpawner replaces Archvile
{
	Default 
	{
		D4DMonsterSpawner.OldMonster "D4OldArchvile";
		D4DMonsterSpawner.NewMonster "D4Archvile";
		Radius 20;
		Height 56;
	}
}

Class StealthArchvileReplacer : ArchvileReplacer  replaces StealthArchvile
{ Default	{ D4DMonsterSpawner.isStealth true;	} }

//===============================================================================
//===============================================================================
//	Cyberdemon
//===============================================================================
//===============================================================================
Class CyberdemonReplacer : D4DMonsterSpawner replaces Cyberdemon
{
	Default 
	{
		D4DMonsterSpawner.isBoss true;
		D4DMonsterSpawner.OldMonster "D4OldCyberdemon";
		D4DMonsterSpawner.NewMonster "D4Cyberdemon";
		Radius 40;
		Height 110;
	}
}

//===============================================================================
//===============================================================================
//	SpiderMastermind 
//===============================================================================
//===============================================================================
Class SpiderMastermindReplacer : D4DMonsterSpawner replaces SpiderMastermind
{
	Default 
	{
		D4DMonsterSpawner.isBoss true;
		D4DMonsterSpawner.OldMonster "D4OldSpiderMastermind";
		D4DMonsterSpawner.NewMonster "D4SpiderMastermind";
		Radius 128;
		Height 100;
	}
}