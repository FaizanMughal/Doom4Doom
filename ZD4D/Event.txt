#include "ZD4D/Spawners.txt"
#include "ZD4D/EventMisc.txt"
#include "ZD4D/EventItemDrops.txt"

Class D4bum : D4Zombieman
{
}

enum ERestrictFlags
{
	RF_NONE =			0,
	RF_NOHEALTH = 		1,
	RF_NOARMOR = 		1 << 1,	//2
	RF_NOBULLETS =		1 << 2,	//4
	RF_NOSHELLS =		1 << 3,	//8
	RF_NOROCKETS =		1 << 4,	//16
	RF_NOCELLS =		1 << 5,	//32
	RF_NOBFGAMMO =		1 << 6,	//64
	RF_NOCARRION =		1 << 7,	//128
	RF_NOCREDITS =		1 << 8,	//256
	RF_NOCRATES =		1 << 9,	//512
	RF_NOUPGRADES =		1 << 10, //1024	- Has no effect in credit mode.
	
	RF_NOCURRENCY = (RF_NOCREDITS|RF_NOCRATES),	
	RF_NOREGAMMO = (RF_NOBULLETS|RF_NOSHELLS|RF_NOROCKETS|RF_NOCELLS),
	RF_NOSPECAMMO = (RF_NOBFGAMMO|RF_NOCARRION),
	RF_NOAMMO = (RF_NOREGAMMO|RF_NOSPECAMMO),
	
	RF_NOPINATAS = (RF_NOHEALTH|RF_NOARMOR|RF_NOAMMO),
	RF_NOTHING = (RF_NOPINATAS|RF_NOCURRENCY|RF_NOUPGRADES)
};

Class D4Restriction play
{
	Class<Actor> mo;
	int Restrictions;
	bool parent, replacement;
}

Class D4DHandler : EventHandler
{
	const Second = 35;
	const TimeTrigger = Second * 5;
	const DefaultDropChance = 200;
	MCHandler MC;

	enum EObjectIndex
	{
		BFGAmmo = 0,
		ChainsawAmmo = 1,
		ObjectIndexMax
	};
	private bool Spawnable[ObjectIndexMax];	
	private bool FirstTime;
	private uint Timer;
	
	AArray 	DemonRunes, Carrion, Beams;
	Array<D4Restriction> RestrictList;
	D4LootContainer LootList;
	D4DDeathStorage Storage[MAXPLAYERS];
	private int UpgradeMode, CurrentUpgradeMode;
	
	bool CheckActorSpawn(EObjectIndex index)			{	return Spawnable[index];	}
	void SetActorSpawn(EObjectIndex index, bool can) 	{	Spawnable[index] = can; 	}
	
	//--------------------------------------------------------------------------
	//--------------------------------------------------------------------------
	
	//--------------------------------------------------------------------------
	//--------------------------------------------------------------------------
	
	void StageCleanUp()
	{
		Timer = TimeTrigger;
	}
	
	override void WorldTick()
	{
		// Clear the lists every TimeTrigger seconds of null pointers.
		Timer++;
		if (Timer >= TimeTrigger)
		{
			DemonRunes.Clean();
			Carrion.Clean();
			Beams.Clean();
		}
	}
	
	override void OnRegister()
	{
		SetOrder(1001);
		Super.OnRegister();
	}
	
	clearscope int GetUpgradeMode()	{	return CurrentUpgradeMode;	}
	override void WorldLoaded(WorldEvent e)
	{
		MC = MCHandler.Fetch();
		DemonRunes = AArray.Create();
		Carrion = AArray.Create();
		Beams = AArray.Create();
		
		
		LootList = D4LootContainer.Create();
		//LootList.SetEntry('D4ZombieMan',					
		LootList.SetEntry('LegendaryZombie',				64, 5, 2, 300,-1,10,0);
		LootList.SetEntry('LegendaryAssaultCaptain',		64, 5, 3, 350,-1,10,0);
		LootList.SetEntry('LegendaryStealthTrooper',		64, 6, 3, 400,-1,10,0);
		LootList.SetEntry('LegendaryCommando',				64,10, 4, 500,-1,10,0);
		LootList.SetEntry('LegendaryImp',					64, 8, 3, 400,-1,10,0);
		LootList.SetEntry('LegendaryPhaseImp',				64, 5, 3, 400,-1,10,0);
		LootList.SetEntry('LegendaryFiend',					64, 6, 4, 500,-1,10,0);
		LootList.SetEntry('LegendaryApparition',			64,15,10, 750,-1,10,1);
		LootList.SetEntry('LegendarySoul',					64, 3, 3, 300,-1,10,0);
		LootList.SetEntry('LegendaryRedeemer',				64,10, 5, 500,-1,10,0);
		LootList.SetEntry('LegendaryNecromancer',			64, 7, 4, 450,-1,10,0);
		LootList.SetEntry('LegendaryHellKnight',			64, 8, 4, 400,-1,10,0);
		LootList.SetEntry('LegendaryCyberNoble',			64, 9, 4, 500,-1,10,0);
		LootList.SetEntry('LegendaryAfrit',					64,10, 5, 450,-1,10,0);
		LootList.SetEntry('LegendaryArchon',				64,10, 5, 600,-1,10,0);
		LootList.SetEntry('LegendaryBehemoth',				64,10, 6, 550,-1,10,0);
		LootList.SetEntry('LegendaryBabySentient',			64,10, 5, 450,-1,10,0);
		LootList.SetEntry('LegendaryTormentor',				64,10, 7, 700,-1, 7,0);
		LootList.SetEntry('LegendaryRevenant',				64,10, 6, 500,-1,12,0);
		LootList.SetEntry('LegendaryVile',					64,10, 7, 800,-1,10,0);
		LootList.SetEntry('LegendarySentient',				64,10,12,1000,-1, 4,0);
		LootList.SetEntry('LegendaryCyberdemon',			64,10,10, 900,-1, 6,0);
		LootList.SetEntry('LegendaryAnnihilator',			64,10,11,1100,-1, 5,0);
		LootList.SetEntry('LegendaryCardinal',				64,10,12,1000,-1,10,1);
		LootList.SetEntry('TrueLegendaryCardinal',			64,10,30,2000,-1,10,4);
		LootList.SetEntry('TrueLegendaryCyberdemon',		64,10,25,2000,-1,10,3);
		LootList.SetEntry('TrueLegendaryCyberdemonNormal',	64,10,20,2000,-1,10,2);
		LootList.SetEntry('TrueLegendarySentient',			64,10,30,2000,-1,10,5);
		LootList.SetEntry('TrueLegendaryBabySentient',		64,10,15,1000,-1,10,1);
		LootList.SetEntry('EnragedLegendaryZombie',			64,10, 5, 400,-1, 7,0);
		LootList.SetEntry('EnragedLegendaryAssaultCaptain',	64,10, 6, 450,-1, 7,0);
		LootList.SetEntry('EnragedLegendaryStealthTrooper',	64,10, 6, 500,-1, 7,0);
		LootList.SetEntry('EnragedLegendaryCommando',		64,10, 7, 500,-1, 6,0);
		LootList.SetEntry('EnragedLegendaryFiend',			64,10, 7, 600,-1, 6,0);
		LootList.SetEntry('EnragedLegendarySoul',			64,10, 5, 400,-1,10,0);
		LootList.SetEntry('EnragedLegendaryRedeemer',		64,10,10, 600,-1, 8,0);
		LootList.SetEntry('EnragedLegendaryHellKnight',		64,10, 8, 650,-1,10,0);
		LootList.SetEntry('EnragedLegendaryCybruiser',		64,10,10, 700,-1,10,1);
		LootList.SetEntry('EnragedLegendaryBehemoth',		64,10, 9, 800,-1, 4,0);
		LootList.SetEntry('EnragedLegendaryBabySentient',	64,10, 8, 700,-1, 3,0);
		LootList.SetEntry('EnragedLegendaryTormentor',		64,10,12, 900,-1, 2,0);
		LootList.SetEntry('EnragedLegendaryRevenant',		64,10, 8, 700,-1, 5,0);
		
		// Use the ERestrictFlags at the top of the file to set restrictions.
		// Most of them should be self explanatory.
		int LostSoulRestrictions = (RF_NOHEALTH|RF_NOARMOR|RF_NOBFGAMMO|RF_NOUPGRADES);
		SetRestrictions("D4LostSoulNoCash", LostSoulRestrictions, true);
		SetRestrictions("D4OldLostSoulNoCash", LostSoulRestrictions, true);
		
		for (int i = 0; i < MAXPLAYERS; i++)	KNMInput[i] = 0;
		// If starting a new game, set the upgrade mode appropriately. 
		
		if (!FirstTime && !e.IsSaveGame)
		{
			CVar Upgrade[2];
			Upgrade[0] = CVar.FindCVar("D4D_UpgradeMode");
			Upgrade[1] = CVar.FindCVar("D4D_CurrentUpgradeMode");
			
			if (Upgrade[0] && Upgrade[1])
			{
				UpgradeMode = Upgrade[0].GetInt();
				Upgrade[1].SetInt(UpgradeMode);
				CurrentUpgradeMode = UpgradeMode;
			}
		}
	}
	
	// Don't clear out the arrays, in the event this is a hub.
	override void WorldUnloaded(WorldEvent e)
	{
		for (int i = 0; i < MAXPLAYERS; i++)	KNMInput[i] = 0;
	}
	
	//--------------------------------------------------------------------------
	//--------------------------------------------------------------------------
	bool IsPlayerPawn(Actor mo)
	{
		return (!mo || mo is "PlayerPawn" || mo.player);
	}
	
	//--------------------------------------------------------------------------
	//--------------------------------------------------------------------------
	// Recording section. Here, we add things that have specifics needs for 
	// tracking so we don't have to rely on iterators. As Graf Zahl stated,
	// an array will go much farther to optimize the crap out of searching
	// over using an iterator in almost all circumstances.
	//
	// Monsters, missiles, items, etc are all recorded in MC_Utility instead
	// of here so they're globally accessible for other mods to use. 
	override void WorldThingSpawned(WorldEvent e)
	{
		Actor mo = e.thing;
		
		if (mo is "D4DBeamBase")
		{
			Beams.Push(D4DBeamBase(mo));
		}
		else if (mo is 'DemonRuneBase')
		{
			DemonRunes.Push(mo);
		}
		else if ((mo.bISMONSTER || mo.bSHOOTABLE) && mo.health > 0)
		{
			let mon = D4AbstractMonster(mo);
			if (mon)	mon.Event = self;
			
			if (D4D_MonsterType >= 2)
			{
				if (mon) // Use the function they have. Lost souls shouldn't gain a level.
				{
					int amt = mon.ChoosePowerLevel();
					if (amt)  mon.SetupPowerLevel(amt);
					return;
				}
				int amt = ChoosePowerLevel();
				mo.SetInventory('D4MonsterUpgrader', amt);
			}
		}
		else if (mo is 'D4PinataCarrion')
		{
			let car = D4PinataCarrion(mo);
			car.Event = self;
			Carrion.Push(mo);
			MC.AddShape(mo, mo, "PINBI0", (0, mo.height / 2.0), 0, (1,1), 'D4D_DrawPinataBorders');
		}
		else if (mo.bNOINTERACTION || mo.bMISSILE)
			return;
		else
		{
			String s = mo.GetClassName();
			s.MakeLower();
			if (s.IndexOf("d4pinata"))
			{
				string texname = "";
					 if (mo is "D4PinataArmor")		texname = "PINBA0";
				else if (mo is "D4PinataHealth")	texname = "PINBB0";
				else if (mo is "D4PinataBullets" ||
						mo is "D4PinataShells" ||
						mo is "D4PinataRockets" ||
						mo is "D4PinataCells" ||
						mo is "D4PinataBulletsZombieman" ||
						mo is "D4PinataShellsShotgunGuy")
													texname = "PINBD0";
				else if (mo is "D4PinataBFG")		texname = "PINBH0";
				else	return;
				
				if (!MC)	MC = MCHandler.Fetch();
				MC.AddShape(mo, mo, texname, (0, mo.height / 2.0), 0, (1,1), 'D4D_DrawPinataBorders');
			}
		}
	}
	
	int ChoosePowerLevel()
	{
		int rng = random(1, 2000);
		if (rng <= 1)	return 7;	
		if (rng <= 5)	return 6;	
		if (rng <= 10)	return 5;	
		if (rng <= 30)	return 4;	
		if (rng <= 60)	return 3;	
		if (rng <= 100)	return 2;	
		if (rng <= 200)	return 1;
		return 0;
	}
	
	//--------------------------------------------------------------------------
	// Called by carrion whenever collected, so carrion won't merge with it.
	//--------------------------------------------------------------------------

	void RemoveCarrion(D4PinataCarrion mo)
	{
		if (mo && Carrion.Size() > 0)
		{
			int i = Carrion.Find(mo);
			if (i < Carrion.Size())
				Carrion.Actors[i] = null;
		}
	}
	//--------------------------------------------------------------------------
	//--------------------------------------------------------------------------
	override void WorldThingDied(WorldEvent e)
	{
		Actor mo = e.Thing;
		
		if (IsPlayerPawn(mo))
			return;
		
		DropPinatas(mo, e.Inflictor);
	}
	
	//--------------------------------------------------------------------------
	//--------------------------------------------------------------------------
	
	override void NetworkProcess(ConsoleEvent e)
	{
		int num = e.Player;
		let plr = players[num].mo;
		
		if (!plr)	
		{	Console.Printf("No player found");	return;	}
		
		if (e.Name == 'ClearFakeCash')
		{
			plr.A_SetInventory("D4DCashFake",0);
			return;
		}
		
		
		// Blocks execution of certain netevent commands that can and will
		// destabilize the game if used improperly. These should only be
		// called from code like the menu.
		if (e.isManual)
		{
			static const Name ProtectedCommands[] =
			{
				//'AkimboSystem'
				'DisableReception',
				'EnableReception',
				'UpdateItems'
			};
			int size = ProtectedCommands.Size();
			for (int i = 0; i < size; i++)
			{
				if (e.Name == ProtectedCommands[i])
				{
					Console.Printf(e.Name.." cannot be called by console.");
					return;
				}
			}
		}
		else
		{
			int arg = e.Args[0];
			if (e.Name == 'D4D_KNM++')			{	KNMInput[arg]++;	return;	}
			else if (e.Name == 'D4D_KNM0')		{	KNMInput[arg] = 0;	return;	}
			else if (e.Name == 'D4D_KNM')		{	KNM(e);				return;	}
		}
		if (e.Name == 'Dash')
		{
			let pl = Doom4Player(plr);
			if (pl)	pl.TryDash();
		}
		else if (e.Name == 'DisableReception')
		{
			deltas[num].receiving = false;
		}
		else if (deltas[num].receiving)
		{
			deltas[num].SetItem(e.Name, e.Args[0], e.Args[1]);
		}
		else if (e.Name == 'EnableReception')
		{
			deltas[num].receiving = true;
		}
		else if (e.Name == 'UpdateItems')
		{
			if (deltas[num].ItemName.Size() > 0)
			{
				for (int i = 0; i < deltas[num].ItemName.Size(); i++)
				{
					Class<Inventory> item = deltas[num].ItemName[i];
					if (item)
					{
						plr.A_SetInventory(item, deltas[num].Count[i]);
						let it = UpgradeItem(plr.FindInventory(item));
						
						if (it)
						{
							it.UpdateItem();
						}
					}
				}
			}
			deltas[num].ItemName.Clear();
			deltas[num].Count.Clear();			
		}
		else if (e.Name == 'SectorLight')		{	SetSectorLight(e);	}		
		else if (e.Name == 'RemoveAmmo')
		{
			RemoveAmmoActors();
		}
		else if (e.Name == 'FindLiveTID')
		{
			FindLiveTID(e);
		}
		else if (e.Name == 'PrintSlots')
		{
			PrintSlots(e);
		}
		else
		{
			static const String KeyWords[] =
			{
				'D4DAddon_',
				'Akimbo_SetLeft_',
				'Akimbo_SetRight_',
				'Rune_'
			};
			int size = KeyWords.Size();
			for (int i = 0; i < size; i++)
			{
				// If it doesn't have the matching command, next please.
				
				
				Name check1 = e.Name.Left(Keywords[i].Length());
				Name check2 = KeyWords[i];
				if (check1 != check2)
					continue;
					
				// It's one of the keywords. Process it.
				String suffix = e.Name.Mid(KeyWords[i].Length(), e.Name.Length());
				
				Switch(i)
				{
					// For external mods to use as they please using their own handlers
					// without interrupting this one.
					Case 0:		// D4DAddon_
						return;
					
					Case 1:		// Akimbo_SetLeft_
					Case 2:		// Akimbo_SetRight_
					{
						bool IsRight = (i == 2);
						let ak = AkimboWeapon(plr.FindInventory("AkimboWeapon",true));
						if (ak)	ak.SetWeaponSide(suffix, IsRight);
						return;
					}
					
					Case 3:
					{
						SummonRune(plr, suffix, e.Args[0]);
						return;
					}
					
					Default:	// Some other command that's not viable.
						break;
				}
			}
			CVar EventPrint = CVar.FindCVar("D4D_NetEventDebug");
			if (EventPrint && EventPrint.GetInt())
			{
				Console.Printf("D4DHandler: "..e.Name.."- Undefined net event");
			}
		}
	}
	
	//==========================================================================
	//==========================================================================
	
	
	//==========================================================================
	//==========================================================================
	// Custom functions
	//==========================================================================
	//==========================================================================
	
	//--------------------------------------------------------------------------
	// IsUpgradeBlacklisted
	//
	// Global function. Checks if the item is directly a set of blacklisted
	// items. Blacklisted items should never be given.
	//--------------------------------------------------------------------------
	static bool IsUpgradeBlacklisted(Class<D4DKeeperItem> Item, bool crate = false)
	{
		if (!Item)	return true;
		static const Name Blacklist[] =
		{
			'UpgradeItem',
			'UpgradeItemSpecial',
			'D4DKeeperItem',
			'D4RuneUpgrade1'
		};
		
		// Make sure it's not directly a blacklisted item.
		String Check = Item.GetClassName();
		for (int j = 0; j < Blacklist.Size(); j++)
			if (Check == Blacklist[j])
				return true;
		
		if (crate)
		{
			if (Check == 'CashPercentBonus')	return true;
		}
		
		// UpgradeBase derived items should never be given. They are merely
		// there for the sake of giving the hint for which weapon they belong to.
		if (Check.IndexOf("UpgradeBase") >= 0)
			return true;
		
		return false;
	}
	
	//--------------------------------------------------------------------------
	//
	//--------------------------------------------------------------------------
	
	static clearscope D4DHandler Fetch()
	{
		return D4DHandler(Find("D4DHandler"));
	}
	
	//==========================================================================
	// Item Restrictions
	//==========================================================================
	
	//--------------------------------------------------------------------------
	// ClearAllRestrictions
	// 
	// Removes all item drop restrictions.
	//--------------------------------------------------------------------------
	void ClearAllRestrictions()
	{
		if (RestrictList.Size() < 1)	return;
		
		
		for (int i = 0; i < RestrictList.Size(); i++)
		{
			D4Restriction res = RestrictList[i];
			if (res) res.Destroy();
		}
		RestrictList.Clear();
	}
	
	//--------------------------------------------------------------------------
	// SetRestrictions
	// 
	// Sets restrictions on what a monster can drop when they die.
	// No flags means it will delete whatever context there is that was 
	// previously established.
	//--------------------------------------------------------------------------
	
	void SetRestrictions(Class<Actor> mo, int flags, bool parent = false)
	{
		if (!mo)	return;
		bool SizeChanged = false;
		for (int i = 0; i < RestrictList.Size(); i++)
		{
			D4Restriction res = RestrictList[i];
			if (!res)
			{
				SizeChanged = true;
				RestrictList.Delete(i);
				i--;
				continue;
			}
			
			if (res.mo == mo)
			{
				if (flags <= 0)
				{
					SizeChanged = true;
					res.Destroy();
					RestrictList.Delete(i);
					continue; // Just to be safe, make sure there's no duplicates.
				}
				res.Restrictions = flags;
				if (SizeChanged)	RestrictList.ShrinkToFit();
				return;
			}
		}
		if (SizeChanged)	RestrictList.ShrinkToFit();
		// Do nothing if the object isn't found and we want to erase it.
		if (flags <= 0)	
			return;
		
		// None found, so make a new one.
		D4Restriction res = new('D4Restriction');
		res.mo = mo;
		res.Restrictions = flags;
		res.parent = parent;
		RestrictList.Push(res);
	}
	
	//--------------------------------------------------------------------------
	// GetRestrictions
	// 
	// Checks for restrictions on monsters and returns the bytes.
	//--------------------------------------------------------------------------
	
	int GetRestrictions(Class<Actor> mo)
	{
		if (!mo || RestrictList.Size() < 1)
			return RF_NONE;
		
		int size = RestrictList.Size();
		for (int i = 0; i < RestrictList.Size(); i++)
		{
			D4Restriction res = RestrictList[i];
			if (!res)	continue;
			
			// Check if the class is directly that, or a child, if the parent 
			// boolean is set in the restriction class.
			if ((mo == res.mo) || (res.parent && CheckParent(mo, res.mo)))
			{
				return res.Restrictions;
			}
		}
		return RF_NONE;
	}
	
	//==========================================================================
	// Misc
	//==========================================================================
	static clearscope bool CheckParent(Class<Object> child, Class<Object> parent)
	{
		if (!child)	return false;
		while (true)
		{
			let check = child.GetParentClass();
			if (child == null || check == null)
				return (parent == null) ? true : false;
			if (check == parent)
				return true;
			child = child.GetParentClass();
		}
		return false;
	}
	
	void DestroyBeams(Actor caller)
	{
		if (caller && Beams && Beams.Size() > 0)
		{
			for (int i = 0; i < Beams.Size(); i++)
			{
				D4DBeamBase mo = D4DBeamBase(Beams.Actors[i]);
				if (mo && (mo.Master == caller) && !mo.bNODAMAGE)
				{
					mo.bNODAMAGE = true;
					mo.SetStateLabel('Death');
				}
			}
		}
	}
	
	static void ConvertBerserkToQuad(D4DBerserk mo)
	{
		if (!mo)	return;
		let quad = D4QuadDamage(Actor.Spawn("D4QuadDamage", mo.pos, ALLOW_REPLACE));
		if (quad)
		{
			quad.ChangeTID(mo.TID);
			quad.A_SetSpecial(mo.Special, mo.args[0], mo.args[1], mo.args[2], mo.args[3], mo.args[4]);
			quad.A_ChangeCountFlags(FLAG_NO_CHANGE, mo.bCOUNTITEM, mo.bCOUNTSECRET);
			quad.ObtainInventory(mo);
			quad.Vel = mo.Vel;
			quad.Angle = mo.Angle;
			quad.Pitch = mo.Pitch;
			quad.SpawnPoint = mo.SpawnPoint;
			quad.ActivationType = mo.ActivationType;
			quad.BlockingMobj = mo.BlockingMobj;
			quad.BlockingLine = mo.BlockingLine;
			quad.FloorZ = mo.FloorZ;
			quad.CeilingZ = mo.CeilingZ;
			quad.DropoffZ = mo.DropoffZ;
			quad.Target = mo.Target;
			quad.Master = mo.Master;
			quad.Tracer = mo.Tracer;
			mo.Destroy();
		}
	}
	
	//==========================================================================
	// Death Storage
	//
	// The purpose of these is to store the player's items when they die in
	// death exits, and restore them on the other side.
	//==========================================================================
	
	override void PlayerRespawned(PlayerEvent e)
	{
		
	}
	
	int FetchPlayerNumber(Actor mo)
	{
		if (!mo || !mo.player)	return-1;
		
		int i = 0;
		for (i = 0; i < MAXPLAYERS; i++)
			if (playeringame[i] && players[i].mo == mo)
				return i;
		
		return -1;
	}
	
	//--------------------------------------------------------------------------
	// Stores the players items into D4DDeathStorage, a static thinker that
	// crosses levels and remembers their inventory. This doesn't take into
	// account any novelties - only the items they had and their amount.
	void StorePlayerItems(Actor mo)
	{
		int i = FetchPlayerNumber(mo);
		if (i < 0)	return;
		
		if (!Storage[i])	Storage[i] = new("D4DDeathStorage");
		Storage[i].Init();
		
		for (let probe = mo.Inv; probe != null; probe = probe.Inv)
		{
			// Powerups, weapons, armor and health don't transfer over since they
			// were dead.
			if (!probe || probe is "Powerup" || probe is "Weapon" || probe is "BasicArmor" ||
				probe is "Health")
				continue;
			
			// Save it.
			Storage[i].SetItem(probe.GetClass(), probe.Amount);
			CVar debug = CVar.FindCVar("D4Debug");
			if (debug && debug.GetInt() > 0)
			{
				String msg = String.Format("%s : %d ", probe.GetClassName(), probe.Amount);
				Console.Printf("%s", msg);
			}
		}
	}
	
	//--------------------------------------------------------------------------
	// 
	void ReturnPlayerItems(Actor mo)
	{
		int i = FetchPlayerNumber(mo);
		if (i < 0 || !Storage[i])	return;
		
		let store = D4DDeathStorage(Storage[i]);
		Class<Inventory> item = null;	int amt = 0;
		
		do
		{
			[item, amt] = store.GetNextItem();
			if (item)
			{
				if (amt > 0)
					mo.SetInventory(item, amt);
				else
				{
					mo.SetInventory(item, 1);
					mo.SetInventory(item, 0);
				}
					
			}
		}
		while (amt > -1);
	}
	
	//==========================================================================
	// ChangeWeapons
	//
	// Sets the forthcoming weapon based on the command sent to it. Applicable
	// commands are:
	// 
	// slot # (0-9)
	// weapnext
	// weapprev
	//==========================================================================
	void ChangeWeapons(ConsoleEvent e) 
	{
		int pnum = e.Player;
		if (!playeringame[pnum])
			return;
			
		PlayerInfo player = players[pnum];

		if (!player) // Return if the player is null
			return;

		Array<String> commandStrings;
		e.Name.Split (commandStrings, ":");
		if (commandStrings.Size() < 2) // Return if the array contains less than two strings
			return;
			
		// At this point, we should have at minimum two strings in this form:
		// [0] = AEoD_SelectWeapon
		// [1] = <command>
		// See InputProcess for the translations (from first to second array)
		
		let ready = player.ReadyWeapon;
		let pending = player.PendingWeapon;
	}
}