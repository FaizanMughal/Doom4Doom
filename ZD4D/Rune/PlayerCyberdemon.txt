//=================================================================================================
//=================================================================================================

//		Cyberdemon

//=================================================================================================
//=================================================================================================
Class CyberdemonRune : DemonRuneBase
{
	Default 
	{
		DemonRuneBase.Morph "CyberdemonMorpher", "IamCyberdemon";
		Inventory.PickupMessage "Cyberdemon rune";
		Scale 0.13;
	}
}

Class CyberdemonMorpher : PowerupGiver
{
	Default 
	{
		Inventory.MaxAmount 1;
		Inventory.Amount 1;
		Powerup.Type "PowerCyberdemonMorph";
		Powerup.Duration -60;
		+Inventory.AutoActivate
	}
	States
	{
		Spawn:
			PLAY A 3;
			Loop;
	}
}

Class PowerCyberdemonMorph : PowerRuneBase
{
	Default 
	{
		PowerMorph.PlayerClass "PlayerCyberdemon";
		PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_UNDOBYDEATH|MRF_UNDOBYDEATHSAVES|MRF_UNDOBYTOMEOFPOWER|MRF_LOSEACTUALWEAPON|MRF_NEWTIDBEHAVIOUR|MRF_UNDOALWAYS|MRF_FAILNOTELEFRAG);
		PowerMorph.UnMorphFlash "DemonRuneMorphOff";
		PowerMorph.MorphFlash "DemonRuneMorphOn";
	}
}


Class PlayerCyberdemon : DemonPlayerBase
{
	double USpeed;
	Default 
	{
		Speed 1;
		Health 4000;
		Radius 25;
		Height 95;
		Mass 800;
		PainChance 20;
		Gravity 0.7;
		Player.JumpZ 10;
		Player.MorphWeapon "D4DCyberdemonWeapon";
		Player.ViewHeight 92;
		Player.AttackZOffset 30;
		Player.SoundClass "PlayerDemonCommon";
		Player.CrouchSprite "CYBR";
		PainSound "Doom4/Cyberdemon/Pain";
		DeathSound "Doom4/Cyberdemon/Death";
		Player.ForwardMove 0.8, 0.4;
		Player.SideMove 0.8, 0.4;
		DamageFactor "Normal", 0.5;
		+NOSKIN
		-PICKUP
	}
	States
	{
	Spawn:
		CYBR A -1;
		Stop;
	See:
		CYBR ABCD 5;
		Loop;
	Pain:
		CYBR G 5 A_Pain();
		Goto Spawn;
	Missile:
	Melee:
		CYBR EF 4;
		Goto Spawn;
	Death:
		TNT1 A 3 A_Scream();
		TNT1 B 3 A_NoBlocking();
		TNT1 CDEFGH 3;
		TNT1 I -1;
		Stop;
	Overlay.IdleSound:
		TNT1 A 35;
		TNT1 A 0 
		{
			if (!GetCvar("D4D_SilentMorphs") && random(1,5)==5)
			{
				A_PlaySound("Doom4/Monsters/Cyberdemon/Idle",5);
			}
		}
		Loop;
	Overlay.Steps:
		TNT1 A 5
		{
			USpeed = vel.Length();
			if (A_CheckFloor("Null"))
			{
				if(USpeed > 10)
				{
					A_PlaySound("Doom4/Cyberdemon/Step",CHAN_AUTO);
					A_SetTics(14);
				}
				else if(USpeed > 5)
				{
					A_PlaySound("Doom4/Cyberdemon/Step",CHAN_AUTO);
					A_SetTics(18);
				}
			}
		}
		Loop;
	}
}

Class D4DCyberdemonWeapon : DoomWeapon
{
	Default 
	{
		Tag "Demonic Rocket Launcher";
	}
	States
	{
	Ready:
		MISG A 1 A_WeaponReady(WRF_DISABLESWITCH);
		Loop;
	Deselect:
		MISG A 1 A_Lower;
		Loop;
	Select:
		MISG A 1 A_Raise;
		Loop;
		
	Fire:
		MISG B 8 A_GunFlash();
		MISG B 1
		{	
			A_FireProjectile("PlayerCyberdemonRocket",0,0,0,10);
			A_SetPitch(pitch-3);
		}
		MISG B 1 A_SetPitch(pitch-3);
		MISG B 3;
		MISG BBBBBB 1 A_SetPitch(pitch+1);
		MISG B 1;
		MISG B 0 A_ReFire("MiniRockets");
		Goto Ready;
		
		Flash:
			MISF A 3 Bright A_Light1();
			MISF B 4 Bright;
			MISF CD 4 Bright A_Light2();
			Goto LightDone;
		
		
		MiniRockets:
			MISG B 3 A_GunFlash("FlashMiniRockets");
			MISG B 1 
			{
				A_FireProjectile("PlayerCyberDemonRocketStream",0,0,0,10);
				A_SetPitch(pitch-2);
			}
			MISG BBBB 1 A_SetPitch(pitch+0.5);
			MISG B 0 A_ReFire("MiniRockets");
			Goto Ready;
			
			FlashMiniRockets:
				MISF A 1 Bright A_Light1();
				MISF B 2 Bright;
				MISF CD 2 Bright A_Light2();
				Goto LightDone;
			
	AltFire:
		MISG B 0 
		{
			A_SpawnItemEx("CyberdemonPlayerMuzzle",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
			A_PlaySound("Doom4/Weapon/GaussGun/Charge2Start",7,1.0);
		}
		MISG BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 A_RailAttack(0,0,0,"","Red",RGF_SILENT|RGF_FULLBRIGHT,0,"NoBloodPuff",0,0,0,1,1.5,1,null,0,0);
		
		MISG A 0 
		{
			//the attack part
			A_RailAttack(1000,1,0,"","",RGF_SILENT|RGF_FULLBRIGHT,0,null,0,0,32765,0,  100.0 ,0.0,"StaticTrailBig");
			
			A_RailAttack(0,0,0,"Orange","",RGF_SILENT|RGF_FULLBRIGHT,0,null,0,0,0,70,1,random(1.0,2.0),null,0,0);
			A_RailAttack(0,0,0,"Orange","",RGF_SILENT|RGF_FULLBRIGHT,0,null,0,0,0,70,1,random(1.0,2.0),null,0,90);
			A_RailAttack(0,0,0,"Orange","",RGF_SILENT|RGF_FULLBRIGHT,0,null,0,0,0,70,1,random(1.0,2.0),null,0,180);
			A_RailAttack(0,0,0,"Orange","",RGF_SILENT|RGF_FULLBRIGHT,0,null,0,0,0,70,1,random(1.0,2.0),null,0,270);
			
			//the other stuff
			A_RemoveChildren(true,RMVF_EVERYTHING,"CyberdemonPlayerMuzzle");
			A_PlaySound("Doom4/Weapon/GaussGun/Charge2Fire",7);
			A_PlaySound("Doom4/Monsters/MastermindLaserFire",8);
			
			A_SetPitch(pitch-7);
		}
		MISG AAAAAAA 1 A_SetPitch(pitch+1);
		Goto Ready;
	Spawn:
		LAUN A -1;
		Stop;
	}
}	

Class CyberdemonPlayerMuzzle : Actor 
{
	const Flags = WARPF_NOCHECKPOSITION|WARPF_COPYPITCH;
	const Distance = 30;
	const YOff = 1;
	const ZOff = 80;
	double  UPos;
	int user_t;
	
	Default 
	{
		Projectile;
		+NOINTERACTION
		RenderStyle "Add";
		Scale 0.0;
		Alpha 0.01;
		+FORCEXYBILLBOARD
		Translation "0:255=%[0,0,0]:[2.0,1.3,0.3]";
	}
	States
	{
	Spawn:
		X060 A 1 BRIGHT NoDelay
		{
			if (master) { pitch = master.pitch; } //Grab the pitch...
			
			// And set a multiplier for taking from the distance so it actually goes well with the gun.
			
			if (pitch > 0) //Looking down
			{	UPos = (sin(pitch) * 12);	}
			else //Looking up
			{	UPos = (sin(pitch) * 6);		}
			
			
			A_Warp(MasterPtr,
				(cos(-pitch)*(Distance)) - UPos,
				YOff,
				(ZOff*GetCrouchFactor(MasterPtr))+(sin(-pitch)*(Distance)),0,Flags|WARPF_INTERPOLATE);
				
			A_SpawnItemEx("CyberdemonPlayerSpark",0,0,-3,sin(-pitch)*frandom(0.1,0.5),frandom(-0.7,-1.3),cos(pitch)*frandom(1,-5.0),0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("CyberdemonPlayerSpark",0,0,-3,sin(-pitch)*frandom(0.1,0.5),frandom(0.7,1.3),cos(pitch)*frandom(1,-5.0),0,SXF_NOCHECKPOSITION);
	
			A_FadeIn(0.014);
			A_SetScale(Scale.X+0.013);
		}
		Loop;
	Nothing:
		TNT1 A 100;
		Stop;
	}
}
Class CyberdemonPlayerSpark : Actor 
{
	Default 
	{
		+NOINTERACTION
		Scale 0.05;
		RenderStyle "Add";
		Translation "0:255=%[0,0,0]:[2.0,1.3,0.3]";
		+FORCEXYBILLBOARD
	}
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_Jump(256,1,2,3,4,5,6,7,8);
		X079 ABCDEFGH 0	A_Jump(256,"Perform");
	Perform:
		"####" "#" 1 Bright 
		{
			A_SetScale(Scale.X+0.01);
			A_FadeOut(0.05);
		}
		Loop;
	}
}
//-----------------------------------------------
// Regular rockets
Class PlayerCyberdemonRocket : Actor 
{
	Default 
	{
		Projectile;
		+EXTREMEDEATH
		Speed 23;
		Radius 6;
		Height 12;
		Scale 0.6;
		Damage 40;
		FastSpeed 30;
		SeeSound "Doom4/Weapon/RocketLauncher/Fire";
		DeathSound "Doom4/Weapon/RocketLauncher/HRExplo";
		Decal "D4ExploScorch";
	}
	States
	{
	Spawn:
		MICR A 3 Light("YellowMediumPoint3") Bright NoDelay A_PlaySound("Doom4/Monsters/RocketFlyLoop",7,0.7,1);
	Moving:
		MICR A 1 Light("YellowMediumPoint3") Bright
		{
			if (GetCvar("HQEffectsWeapons"))
			{
				A_SpawnItemEx("D4RocketTrailFire1",-5,0,0,0,frandom(0.3,-0.3),frandom(0.3,-0.3),0,SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketTrailSmoke",-5,random(5,-5),random(5,-5),0,0,0,0,SXF_NOCHECKPOSITION);	
			}
			A_SpawnItemEx("D4RocketTrailFire2",-5,0,0,0,0,0,0,SXF_NOCHECKPOSITION);	//spawns even if HQ effects are off, for visibility
		}
		Loop;
	Death:
		TNT1 A 0 
		{
			A_Explode(192,256,0);
			A_QuakeEx(2,2,2,15,0,384,"",QF_SCALEDOWN, falloff: 1);
			
			if (GetCvar("HQEffectsWeapons"))
			{
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
			}
			A_SpawnItemEx("D4RocketExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		}
		TNT1 A 50
		{
			bNoInteraction = true;
		}
		Stop;
	}
}

//-----------------------------------------------
// Stream of rockets
Class PlayerCyberDemonRocketStream : Actor 
{
	Default 
	{
		Projectile;
		+EXTREMEDEATH
		Speed 21;
		Radius 6;
		Height 12;
		Scale 0.4;
		Damage 18;
		SeeSound "Doom4/Weapon/RocketLauncher/Fire";
		DeathSound "Doom4/Weapon/RocketLauncher/HRExplo";
		Decal "D4ExploScorchSmall";
	}
	States
	{
	Spawn:
		MICR A 3 Light("YellowMediumPoint3") Bright NoDelay A_PlaySound("Doom4/Monsters/RocketFlyLoop",7,0.7,1);
	Moving:
		MICR A 1 Light("YellowMediumPoint3") Bright 
		{
			if (GetCvar("HQEffects"))
			{
				A_SpawnItemEx("CDRocketFlare",-5,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			}
			else
			{
				A_SpawnItemEx("CDRocketFlare",-5,0,0,0,0,0,0,SXF_NOCHECKPOSITION,64); //still spawns constantly for visibility
			}
		}
		Loop;
	Death:
		TNT1 A 0 
		{
			A_Explode(92,128,0);
			if (GetCvar("D4D_quakexplosions")==1)
			{
				A_QuakeEx(2,2,2,10,0,192,"",QF_SCALEDOWN, falloff: 1);
			}
			if (GetCvar("HQEffects"))
			{
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
				A_SpawnItemEx("D4RocketExplosionSmoke",0,0,0,frandom(0.5,1.5),frandom(0.5,1.5),frandom(0.5,1.5),random(0,359),SXF_NOCHECKPOSITION);
			}
			A_SpawnItemEx("CyberdemonRocketExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		}
		TNT1 A 50
		{
			bNoInteraction = true;
		}
		Stop;
	}
}