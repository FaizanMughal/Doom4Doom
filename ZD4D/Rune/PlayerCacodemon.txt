//=================================================================================================
//=================================================================================================

//		Cacodemon

//=================================================================================================
//=================================================================================================
Class CacodemonRune : DemonRuneBase
{
	Default 
	{
		Inventory.PickupMessage "Cacodemon rune";
	}
	States
	{
	Pickup:
		TNT1 A 0 
		{
			if (CountInv("IamADemonToken") || CountInv("PowerInvulBase") || countinv("D4DoomDollHands") || countinv("BusyToken"))
			{
				return false;
			}
			else
			{
				A_GiveInventory("SoundStopper",1); //stops any sound (mostly for the looping ones)
				A_GiveInventory("HealthArmorSaver",1);
				A_GiveInventory("IamADemonToken",1);
				A_GiveInventory("IamCacodemon",1);
				A_GiveInventory("CacodemonMorpher",1);
				return true;
			}
		}
		Stop;
	}
}

Class CacodemonMorpher: PowerupGiver
{
	Default 
	{
		Inventory.MaxAmount 1;
		Inventory.Amount 1;
		Powerup.Type "PowerCacodemonMorph";
		Powerup.Duration -60;
		+Inventory.AutoActivate
	}
	States
	{
		Spawn:
			PLAY A 3;
			Loop;
	}
}

Class PowerCacodemonMorph : PowerRuneBase
{
	Default 
	{
		PowerMorph.PlayerClass "PlayerCacodemon";
		PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_UNDOBYDEATH|MRF_UNDOBYDEATHSAVES|MRF_UNDOBYTOMEOFPOWER|MRF_LOSEACTUALWEAPON|MRF_NEWTIDBEHAVIOUR|MRF_UNDOALWAYS|MRF_FAILNOTELEFRAG);
		PowerMorph.UnMorphFlash "DemonRuneMorphOff";
		PowerMorph.MorphFlash "DemonRuneMorphOn";
	}
}
Class FlyingMonsterPowerup : PowerupGiver
{
	Default 
	{
		+FLOATBOB
		Powerup.Type "PowerFlight";
		powerup.duration -60;
		+Inventory.AutoActivate
	}
	states
	{
	Spawn:
		PLAY A -1;
		Stop;
	}
}


Class PlayerCacodemon : DemonPlayerBase
{
	Default 
	{
		Speed 1;
		Friction 1.06;
		Health 400;
		Radius 20;
		Height 56;
		Mass 500;
		PainChance 40;
		Gravity 0.5;
		Player.JumpZ 10;
		Player.MorphWeapon "D4DCacodemonWeapon";
		Player.ViewHeight 53;
		Player.AttackZOffset 15;
		Player.ForwardMove 0.65, 0.325;
		Player.SideMove 0.5, 0.25;
		Player.CrouchSprite "CAC2";
		Player.SoundClass "PlayerDemonCommon";
		PainSound "Doom4/Monsters/Cacodemon/Pain";
		DeathSound "Doom4/Monsters/Cacodemon/Death";
		Scale 0.65;
		DamageFactor "Normal", 0.4;
		+NOSKIN;
		-PICKUP;
	}
	States
	{
	Spawn:
		CAC2 A -1;
		Stop;
	See:
		CAC2 A 3;
		Loop;
	Pain:
		CAC2 E 5 A_Pain();
		Goto Spawn;
	Missile:
	Melee:
		CAC2 BC 3;
		CAC2 D 3 Bright;
		Goto Spawn;
	Death:
		CAC2 G 5;
		CAC2 H 5 A_Scream();
		CAC2 I 5;
		CAC2 J 5;
		CAC2 K 5 A_NoBlocking();
		CAC2 L -1 A_SetFloorClip();
		Stop;
	Overlay.IdleSound:
		TNT1 A 35;
		TNT1 A 0 
		{
			if (!GetCvar("D4D_SilentMorphs") && random(1,8)==8)
			{
				A_PlaySound("Player/Demon/Demon/Idle",5);
			}
		}
		Loop;
	}
}





Class D4DCacodemonWeapon : DoomWeapon
{
	Default 
	{
		Tag "Demonic Big Mouth";
	}
	States
	{
	Ready:
		TNT1 A 0 A_GiveInventory("FlyingMonsterPowerup",1);
		TNT1 A 1 A_WeaponReady(WRF_DISABLESWITCH);
		Wait;
	Deselect:
		TNT1 A 1 A_Lower;
		Loop;
	Select:
		TNT1 A 1 A_Raise;
		Wait;
	Fire:
		TNT1 A 0 
		{
			A_FireProjectile("D4PlayerCacodemonBall",0,0,0,10);
			A_PlaySound("Doom4/Monsters/Cacodemon/Attack1",CHAN_WEAPON);
		}
		TNT1 AAAA 1 A_SetPitch(pitch-1, SPF_INTERPOLATE);
		TNT1 A 4;
		TNT1 AAAAAAAA 1 A_SetPitch(pitch+0.5, SPF_INTERPOLATE);
		TNT1 A 9;
		Goto Ready+1;
	User2:
	AltFire:
		TNT1 AAAAAAAA 1 A_SetPitch(pitch-0.8, SPF_INTERPOLATE);
		TNT1 A 0 A_PlaySound("Doom4/Monsters/Cacodemon/Attack2",7);
		TNT1 AA 1 A_SetPitch(pitch-0.8, SPF_INTERPOLATE);
		TNT1 AAAA 1 A_SetPitch(pitch+4, SPF_INTERPOLATE);
		TNT1 A 0 A_CustomPunch(random(100,300),1,0,"DemonPuffMelee",80,0,0,null,"","");
		TNT1 AAAAAAAAAA 1 A_SetPitch(pitch-0.8, SPF_INTERPOLATE);
		TNT1 A 0 A_Refire;
		Goto Ready+1;
		
	Flash:
		SHTF A 1 Bright A_Light1();
		SHTF B 1 Bright A_Light2();
		Goto LightDone;
	Spawn:
		SHOT A -1;
		Stop;
	}
}	
Class D4PlayerCacodemonBall : Actor 
{
	Default 
	{
		Radius 6;
		Height 8;
		Speed 24;
		DamageFunction (60*random(2,6));
		Projectile;
		+RANDOMIZE;
		RenderStyle "Add";
		Alpha 0.99;
		SeeSound "caco/attack";
		DeathSound "caco/shotx";
		+FORCEXYBILLBOARD
		Scale 0.26;
		Translation "0:255=%[0.0,0.0,0.0]:[1.2,0.0,0.5]";
		Decal "CacoDecal";
		+EXTREMEDEATH
	}
	States
	{
	Spawn:
		X016 ABCDEFGHIJKLMNOPQRSTUVWXY 1 Light("PurpleMediumPoint2") bright 
		{
			if (GetCvar("HQEffectsWeapons"))
			{
				A_SpawnItemEx("D4CacoBallTrail",-5,0,0,0,0,0,0,32);
				A_SpawnParticle("F5008B",SPF_FULLBRIGHT,random(20,40),random(2,4),0,0,0,0,frandom(1.5,-1.5),frandom(1.5,-1.5),frandom(0.5,2.5),0,0,-0.1);
			}
		}
		Loop;
	Death:
		TNT1 A 0 
		{
			A_Explode(64,128,0);
			A_PlaySound("Doom4/Other/FireballHit1"); //in addition to the deathsound
			A_SpawnItemEx("D4CacoBallExplo",0,0,0,0,0,0,0,32);
			if (GetCvar("HQEffectsWeapons"))
			{
				A_SpawnItemEx("D4CacoExploFlash",0,0,0,0,0,0,0,32);
				A_SpawnParticle("F5008B",SPF_FULLBRIGHT,random(20,40),random(3,5),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
				A_SpawnParticle("F5008B",SPF_FULLBRIGHT,random(20,40),random(3,5),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
				A_SpawnParticle("F5008B",SPF_FULLBRIGHT,random(20,40),random(3,5),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
				A_SpawnParticle("F5008B",SPF_FULLBRIGHT,random(20,40),random(3,5),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
				A_SpawnParticle("F5008B",SPF_FULLBRIGHT,random(20,40),random(3,5),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);
				A_SpawnParticle("F5008B",SPF_FULLBRIGHT,random(20,40),random(3,5),0,0,0,0,frandom(2.5,-2.5),frandom(2.5,-2.5),frandom(1,5),0,0,-0.3);	
			}
		}
		TNT1 A 0 A_SetTranslucent(0.01,1);
		TNT1 A 2 bright Light("PurpleBigPoint3");
		TNT1 A 2 bright Light("PurpleMediumPoint1");
		TNT1 A 2 bright Light("PurpleMediumPoint2");
		TNT1 A 2 bright Light("PurpleMediumPoint3");
		TNT1 A 2 bright Light("PurpleSmallPoint1");
		TNT1 A 2 bright Light("PurpleSmallPoint2");
		TNT1 A 2 bright Light("PurpleSmallPoint3");
		Stop;
	}
}